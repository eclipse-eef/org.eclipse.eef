grammar org.eclipse.emf.eef.ModelingBot with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

import "http://www.eclipse.org/emf/eef/mbot/1.0.0" as mbot
import "http://www.eclipse.org/emf/eef/test/EclipseActions/1.0.0" as EclipseActions
import "http://www.eclipse.org/emf/eef/test/EEFActions/1.0.0" as EEFActions
import "http://www.eclipse.org/emf/eef/extended/editor/1.0.0" as EEFExtended

Scenario returns mbot::Scenario: 
	'scenario' name=EString '{'
		processings+=Processings*
	'}';

Processings returns mbot::Processing:
	Details | Wizard | PropertiesView | Action
;

PropertiesView returns mbot::PropertiesView:
	'propertiesView' (name=EString)? '{'
		processings+=SubProcessings*
	'}'
;

Wizard returns mbot::Wizard:
	'wizard' (name=EString)? '{'
		processings+=SubProcessings*
	'}'
;


Details returns mbot::DetailsPage:
	'detailsPage' (name=EString)? '{'
		processings+=SubProcessings*
	'}'
;


SubProcessings returns mbot::Processing:
	Wizard | Details | PropertiesView | Action
;

Action returns mbot::Action:
	EclipseAction | EEFAction
;

EclipseAction returns EclipseActions::EclipseAction:
	CreateProject | CreateModel | Save | RemoveProject
;

CreateProject returns EclipseActions::CreateProject:
	'createProject' (projectName=EString)
;

RemoveProject returns EclipseActions::RemoveProject:
	'deleteProject' (projectName=EString)
;

ReferenceableObject returns EEFExtended::ReferenceableObject:
	CreateModel | Add
;

CreateModel returns EclipseActions::CreateModel:
	'createObject' referenceName=ID ':' rootURI=EString 
	'inResource' modelName=EString 
	'at' path=EString 
;

Save returns EclipseActions::Save:
	'save' {EclipseActions::Save}
;

EEFAction returns EEFActions::EEFAction:
	Check | Add | SetAttribute
;

Check returns EEFActions::Check:
	'check!' {EEFActions::Check}
;

Add returns EEFActions::Add:
	'createObject' referenceName=ID ':' typeURI=EString 
		'inElement' referenceableObject=[EEFExtended::ReferenceableObject]
		'inFeature' eContainingFeatureURI=EString  
;

SetAttribute returns EEFActions::SetAttribute:
	'set' referenceableObject=[EEFExtended::ReferenceableObject] 
		'values' (values+=EString)("," values+=EString)*
		'inFeature' eContainingFeatureURI=EString 
		('usingWidget' propertiesEditionElementURI=EString)?
;

EString returns ecore::EString:
	STRING | ID;
